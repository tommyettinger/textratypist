buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.22.0'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'

compileJava {
    sourceCompatibility = 7
    targetCompatibility = 7
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(7)
    }
}

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

def projectName = 'textratypist'
version "$VERSION_NAME"
group 'com.github.tommyettinger'

//api "com.github.tommyettinger:textratypist:0.7.5"

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            // The -quiet is because of some sort of weird JDK JavaCompiler bug:
            // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
            options.addStringOption('Xdoclint:none,-missing', '-quiet')
        }
    }
}

sourceSets.test.resources.srcDirs = [file('src/test/resources').path, file('knownFonts').path]

javadoc.destinationDir = file('docs/apidocs')

processResources.duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
processTestResources.duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    api "com.github.tommyettinger:regexodus:$regexodusVersion"
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    // don't require audio if the game doesn't need it; we don't use audio here.
    testImplementation ("com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"){
        exclude group: "org.jcraft", module: "jorbis"
        exclude group: "org.lwjgl", module: "lwjgl-openal"
    }
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testImplementation "com.rafaskoberg.gdx:typing-label:1.3.0"
    testImplementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    testImplementation "com.github.raeleus.stripe:freetype:1.4.1"
    testImplementation "com.github.raeleus.TenPatch:tenpatch:5.2.3"
    testImplementation "com.github.tommyettinger:anim8-gdx:$anim8Version"

}
