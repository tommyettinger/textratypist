buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.17.0'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'

sourceCompatibility = 1.7

def projectName = 'textratypist'
version "$VERSION_NAME"
group 'com.github.tommyettinger'

//api "com.github.tommyettinger:textratypist:0.0.1-SNAPSHOT"

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            // The -quiet is because of some sort of weird JDK JavaCompiler bug:
            // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
            options.addStringOption('Xdoclint:none,-missing', '-quiet')
        }
    }
}

if(JavaVersion.current().isJava9Compatible()) {
    allprojects {
        tasks.withType(JavaCompile) {
            options.release.set(7)
        }
    }
}

sourceSets.test.resources.srcDirs = [ file('src/test/resources').path ]

javadoc.destinationDir = file('docs/apidocs')

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
  api "com.github.tommyettinger:textramode:$textraVersion"
  api "com.github.tommyettinger:regexodus:$regexodusVersion"
  api "com.badlogicgames.gdx:gdx:$gdxVersion"
  testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
  testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}
