plugins {
  id 'java'
  id 'org.gretty' version '3.1.0'
}

gretty {
  contextPath = '/'
  extraResourceBase 'build/dist/webapp'
}

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
project.ext.mainClassName = 'com.github.tommyettinger.teavm.TeaVMBuilder'
eclipse.project.name = appName + '-teavm'

// This must be at least 11, and no higher than the JDK version this project is built with.
targetCompatibility = 11
// This should probably be equal to targetCompatibility, above. This only affects the TeaVM module.
sourceCompatibility = 11


dependencies {
  implementation "com.github.xpenatan.gdx-teavm:backend-teavm:$gdxTeaVMVersion"
  implementation project(':core')

}

task buildJavaScript(dependsOn: classes, type: JavaExec) {
  setDescription("Transpile bytecode to JavaScript via TeaVM")
  mainClass.set(project.mainClassName)
  setClasspath(sourceSets.main.runtimeClasspath)
}
build.dependsOn buildJavaScript

task run(dependsOn: [buildJavaScript, ":teavm:jettyRun"]) {
  setDescription("Run the JavaScript application hosted via a local Jetty server at http://localhost:8080/")
}
